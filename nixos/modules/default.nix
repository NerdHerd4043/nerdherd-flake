{
  config,
  lib,
  self,
  ...
}:
let
  cfg = config.herd;
  inherit (lib)
    concatStringsSep
    match
    mkDefault
    mkEnableOption
    mkIf
    mkOverride
    ;

  mkTrue = mkOverride 1100 true;

  # Takes the lastModifiedDate which is YYYYMMDDHHmmss
  # and converts it into YYYY-MM-DD.
  # Assumes source is entirely digits.
  date = concatStringsSep "-" (match "^(.{4})(.{2})(.{2}).*$" self.lastModifiedDate);
in
{
  imports = [
    ./acme.nix
    ./audio.nix
    ./boot.nix
    ./ddclient.nix
    ./minecraft-server.nix
    ./networking.nix
    ./nginx.nix
    ./nix.nix
    ./packages.nix
    ./tailscale.nix
    ./wiki.nix
  ];

  options.herd.defaults = mkEnableOption "herd defaults" // {
    default = true;
  };

  config = {
    herd = mkIf cfg.defaults {
      audio.enable = mkTrue;
      boot.enable = mkTrue;
      networking.enable = mkTrue;
      nix.enable = mkTrue;
      packages.enable = mkTrue;
    };

    system.image = {
      id = mkDefault "cowflake";
      version = mkDefault "${date}-${self.shortRev or self.dirtyShortRev}";
    };
  };
}
